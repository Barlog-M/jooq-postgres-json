buildscript {
	ext {
		kotlinVer = '1.1.51'
		springBootVer = '1.5.8.RELEASE'
		kotlinLoggingVer = '1.4.6'
		postgesqlVer = '42.1.4'
		jFairyVer = '0.5.8'
		flywayVer = '4.2.0'

		propdepsPluginVer = '0.0.10.RELEASE'
		versionPluginVer = '0.15.0'
		dockerPluginVer = '0.13.0'
		dockerComposePluginVer = '0.4.2'
		jooqPluginVer = '2.0.6'
	}

	repositories {
		jcenter()
		maven { url 'https://plugins.gradle.org/m2/' }
		maven { url 'http://repo.spring.io/plugins-release' }
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVer"
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVer"
		classpath "io.spring.gradle:propdeps-plugin:$propdepsPluginVer"
		// gradle dependencyUpdates -Drevision=release
		classpath "com.github.ben-manes:gradle-versions-plugin:$versionPluginVer"
		classpath "gradle.plugin.com.boxfuse.client:flyway-release:$flywayVer"
		classpath "nu.studer:gradle-jooq-plugin:$jooqPluginVer"
		classpath "gradle.plugin.com.palantir.gradle.docker:gradle-docker:$dockerPluginVer"
		classpath "com.avast.gradle:docker-compose-gradle-plugin:$dockerComposePluginVer"
	}
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'propdeps'
apply plugin: 'propdeps-idea'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.palantir.docker'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'nu.studer.jooq'
apply plugin: 'docker-compose'

group = 'li.barlog.postgres-performance'
version = '0.1-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

mainClassName = 'li.barlog.AppKt'

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		jvmTarget = "1.8"
	}
}

sourceSets {
	main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
}

repositories {
	jcenter()
}

configurations {
	all*.exclude module: 'tomcat-jdbc'
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVer"
	//compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVer"

	compile "io.github.microutils:kotlin-logging:$kotlinLoggingVer"

	compile 'org.springframework.boot:spring-boot-starter'

	compile 'com.fasterxml.jackson.module:jackson-module-kotlin'
	compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

	compile 'org.springframework.boot:spring-boot-starter-jooq'
	compile "org.postgresql:postgresql:$postgesqlVer"
	jooqRuntime "org.postgresql:postgresql:$postgesqlVer"

	compile "org.flywaydb:flyway-core:$flywayVer"

	compile 'com.zaxxer:HikariCP'

	compile "io.codearte.jfairy:jfairy:$jFairyVer"

	testCompile 'org.springframework.boot:spring-boot-starter-test'

	kapt 'org.springframework.boot:spring-boot-configuration-processor'
	optional "org.springframework.boot:spring-boot-configuration-processor"
}

jar {
	archiveName = 'app.jar'
}

tasks.withType(Test) {
	maxParallelForks = Runtime.runtime.availableProcessors()
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.3'
	distributionType = Wrapper.DistributionType.ALL
}

bootRepackage.mustRunAfter clean

// gradle flywayMigrate
// gradle flywayClean
// gradle flywayInfo
// gradle flywayValidate

flyway {
	url = 'jdbc:postgresql://localhost:5432/postgres'
	user = 'postgres'
	password = 'password'
}

// gradle generateDbJooqSchemaSource --rerun-tasks

jooq {
	db(sourceSets.main) {
		jdbc {
			driver = 'org.postgresql.Driver'
			url = 'jdbc:postgresql://localhost:5432/postgres'
			user = 'postgres'
			password = 'password'
		}
		generator {
			database {
				name = 'org.jooq.util.postgres.PostgresDatabase'
				inputSchema = 'public'
				includes = '.*'
				customTypes {
					customType {
						name = 'Person'
						type = 'li.barlog.model.Person'
						binding = 'li.barlog.jooq.PersonBinding'
					}
				}
				forcedTypes {
					forcedType {
						userType = 'li.barlog.model.Person'
						binding = 'li.barlog.jooq.PersonBinding'
						expression = '.*\\.person.data'
					}
				}
			}
			generate {
				relations = true
				deprecated = false
				records = true
				immutablePojos = true
				fluentSetters = true
				javaTimeTypes = true
			}
			target {
				packageName = 'li.barlog.model.jooq'
				directory = 'src/main/java'
			}
		}
	}
}
cleanGenerateDbJooqSchemaSource.enabled = false
generateDbJooqSchemaSource.enabled = false

// https://github.com/avast/docker-compose-gradle-plugin
// gradle composeUp
// gradle composeDown
dockerCompose {
	useComposeFiles = ["${rootDir}/docker/docker-compose.yml"]
}
dockerCompose.isRequiredBy(test)

// https://github.com/palantir/gradle-docker
// gradle docker
docker {
	dependsOn jar, bootRepackage
	name 'app'
	tags 'latest'
	files jar.outputs
	dockerfile file('src/main/docker/Dockerfile')
	buildArgs([
		JAVA_OPTS: '-XX:+UnlockExperimentalVMOptions -XX:+UseCGroupMemoryLimitForHeap',
		PORT     : "8080"
	])
	pull true
}

task stage(dependsOn: ['build', 'clean'])
